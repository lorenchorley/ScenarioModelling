"Name"    = 'Expression Grammar'
"Author"  = 'LChorley'
"Version" = '1.0'
"About"   = ''

"Start Symbol" = <Program>
"Case Sensitive" = False 

{WS} = {Whitespace} - {CR} - {LF}
Whitespace = {WS}+
NewLine = {CR}{LF}|{CR}

<nl>     ::= NewLine <nl>
          |  NewLine

<nlo>    ::= NewLine <nlo>
          |

! ----------------------------------------------------------------- Sets

{FirstIdentifierCharacter} = {Letter} + [_]
{OtherIdentifierCharacter} = {AlphaNumeric} + [_éèàäëïöüùçâêîôûÇÄÅÉæÆÖÜøØƒáíóúñÑÁÂÀãÃðÐÊËÈiÍÎÏÌÓßÔÒõÕµþÞÚÛÙýÝ'']
{StringCharacter}          = {All Printable} - ["\] + {HT} + {CR} + {LF}


! ----------------------------------------------------------------- Terminals

!'[a-zA-Z_][a-zA-Z_0-9]*'                                              Identificateur;
Identifier = {FirstIdentifierCharacter}{OtherIdentifierCharacter}*

!'[^\"]*'                                                              ConstCh;
!'\"'                                                                  %ignore, %pop;
StringLiteral = '"' {StringCharacter}* '"'


! ===========================================================================
! C.1.8 Literals 
! ===========================================================================

<String> ::= Identifier
          |  StringLiteral


! ===========================================================================
! C.2.4 Expressions 
! ===========================================================================

<Program> ::= <nlo> <Exp> <nlo>
           |  <nlo>

<Exp> ::= <AndOr Exp>
       
<AndOr Exp>
       ::= <AndOr Exp> 'AND' <Is Exp>
        |  <AndOr Exp> '&&' <Is Exp>
        |  <AndOr Exp> 'OR' <Is Exp>
        |  <AndOr Exp> '||' <Is Exp>
        |  <Is Exp>
        
<Is Exp>
       ::= <Is Exp> '==' <Value Exp>
        |  <Is Exp> '!=' <Value Exp>
        |  <Value Exp>

<Value Exp>
        ::= <Value>
         |  <Function>
         |  <IsRelated>
         |  <IsNotRelated>
         |  '(' <Exp> ')'

<Function> ::= <String> '(' <Args> ')'

<Args> ::= <Args> ',' <Exp>
        |  <Exp>
        |

<IsRelated> ::= <String> '-?>' <String>
             |  <String> '-?>' <String> ':' <String>
             
<IsNotRelated> ::= <String> '-!>' <String>
                |  <String> '-!>' <String> ':' <String>                
        
<Value> ::= <ValueComposite>

<ValueComposite> ::= <ValueComposite> '.' <String>
                  |  <String>
